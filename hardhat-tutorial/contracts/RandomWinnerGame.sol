// SPDX-License-Identifier: MIT
pragma solidity ^0.8.4;

import "@openzeppelin/contracts/access/Ownable.sol";
import "@chainlink/contracts/src/v0.8/VRFV2WrapperConsumerBase.sol";

contract RandomWinnerGame is VRFV2WrapperConsumerBase, Ownable {

    // Chainlink variables
    // The amount of LINK to send with the request
    uint256 public fee;
    // Depends on the number of requested values that you want sent to the
    // fulfillRandomWords() function. Test and adjust
    // this limit based on the network that you select, the size of the request,
    // and the processing of the callback request in the fulfillRandomWords()
    // function.
    uint32 callbackGasLimit = 100000;
    // The default is 3, but you can set this higher.
    uint16 requestConfirmations = 3;
    // For this example, retrieve 1 random values in one request.
    // Cannot exceed VRFV2Wrapper.getConfig().maxNumWords.
    uint32 numWords = 1;

    // Address of the players
    address[] public players;
    //Max number of players in one game
    uint8 maxPlayers;
    // Variable to indicate if the game has started or not
    bool public gameStarted;
    // the fees for entering the game
    uint256 entryFee;
    // current game id
    uint256 public gameId;
    // Refer the configurations of chainlink token address and 
    // chainlink VRF wrapper address based on your deployed network
    // Refer this: https://docs.chain.link/vrf/v2/direct-funding/supported-networks/#polygon-matic-mumbai-testnet
    // Address LINK - hardcoded for mumbai testnet
    address linkAddress; //0x326C977E6efc84E512bB9C30f76E30c160eD06FB
    // Address Wrapper - hardcoded for mumbai testnet
    address wrapperAddress; //0x99aFAf084eBA697E584501b8Ed2c0B37Dd136693

    // emitted when the game starts
    event GameStarted(uint256 gameId, uint8 maxPlayers, uint256 entryFee);
    // emitted when someone joins a game
    event PlayerJoined(uint256 gameId, address player);
    // emitted when the game ends
    event GameEnded(uint256 gameId, address winner,uint256 requestId);

   /**
   * constructor inherits a VRFV2WrapperConsumerBase and initiates the values for addresses, fee and gameStarted
   * @param vrfV2Wrapper address of VRF V2 Wrapper contract on Polygon Mumbai
   * @param linkToken address of LINK token contract on Polygon Mumbai
   * @param vrfFee the amount of matic to join the game
   */
    constructor(address vrfV2Wrapper, address linkToken, uint256 vrfFee)
    VRFV2WrapperConsumerBase(linkToken, vrfV2Wrapper) {
        wrapperAddress = vrfV2Wrapper;
        linkAddress = linkToken;
        fee = vrfFee;
        gameStarted = false;
    } 

    /**
    * startGame starts the game by setting appropriate values for all the variables
    */
    function startGame(uint8 _maxPlayers, uint256 _entryFee) public onlyOwner {
        // Check if there is a game already running
        require(!gameStarted, "Game is currently running");
        // empty the players array
        delete players;
        // set the max players for this game
        maxPlayers = _maxPlayers;
        // set the game started to true
        gameStarted = true;
        // setup the entryFee for the game
        entryFee = _entryFee;
        gameId += 1;
        emit GameStarted(gameId, maxPlayers, entryFee);
    }

    /**
    joinGame is called when a player wants to enter the game
     */
    function joinGame() public payable {
        // Check if a game is already running
        require(gameStarted, "Game has not been started yet");
        // Check if the value sent by the user matches the entryFee
        require(msg.value == entryFee, "Value sent is not equal to entryFee");
        // Check if there is still some space left in the game to add another player
        require(players.length < maxPlayers, "Game is full");
        // add the sender to the players list
        players.push(msg.sender);
        emit PlayerJoined(gameId, msg.sender);
        // If the list is full start the winner selection process
        if(players.length == maxPlayers) {
            getRandomWinner();
        }
    }

    /**
    * fulfillRandomness is called by VRFCoordinator when it receives a valid VRF proof.
    * This function is overrided to act upon the random number generated by Chainlink VRF.
    * @param requestId  this ID is unique for the request we sent to the VRF Coordinator
    * @param randomness this is a random uint256 generated and returned to us by the VRF Coordinator
   */
    function fulfillRandomWords(uint256 requestId, uint256[] memory randomness) internal virtual override  {
        // We want out winnerIndex to be in the length from 0 to players.length-1
        // For this we mod it with the player.length value
        uint256 winnerIndex = randomness[0] % players.length;
        // get the address of the winner from the players array
        address winner = players[winnerIndex];
        // send the ether in the contract to the winner
        (bool sent,) = winner.call{value: address(this).balance}("");
        require(sent, "Failed to send Ether");
        // Emit that the game has ended
        emit GameEnded(gameId, winner, requestId);
        // set the gameStarted variable to false
        gameStarted = false;
    }

    /**
    * getRandomWinner is called to start the process of selecting a random winner
    */
    function getRandomWinner() private returns (uint256 requestId) {
        // LINK is an internal interface for Link token found within the VRFWrapper
        // Here we use the balanceOF method from that interface to make sure that our
        // contract has enough link so that we can request the VRFCoordinator for randomness
        require(LINK.balanceOf(address(this)) >= fee, "Not enough LINK");
        // Make a request to the VRF coordinator.
        // requestRandomness is a function within the VRFWrapper
        // it starts the process of randomness generation
        return requestRandomness(callbackGasLimit, requestConfirmations, numWords);
    }

     // Function to receive Ether. msg.data must be empty
    receive() external payable {}

    // Fallback function is called when msg.data is not empty
    fallback() external payable {}
}